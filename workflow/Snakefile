from pathlib import Path
import os
from functools import lru_cache as cache
import pandas as pd
import numpy as np
from snakemake.utils import min_version
from gambit.util.io import read_lines, write_lines

min_version("7.0")

wildcard_constraints:
	genomeset="set[1234]",
	k="\d+",
	prefix="[ACGT]+",


### Configuration ###

configfile: "config/config.yaml"

# Running in test mode?
# Note - can't set config value to a bool from the command line, just check for truthiness
TEST = bool(config.get('test', False))

if TEST:
	print("Test mode enabled!")
	# Override default config file with values in test file
	configfile: "config/config-test.yaml"

# Environment
os.environ.update(
	GAMBIT_DB_PATH="resources/gambit-db",
	MPLCONFIGDIR=os.path.abspath('config/matplotlib'),
	MATPLOTLIBRC=os.path.abspath('config/matplotlib/matplotlibrc'),
)

# Main GAMBIT parameters
K = config['gambit']['k']
PREFIX = config['gambit']['prefix']

# Main genome sets compared in figures 1 and 2
COMPARISON_GENOME_SETS = ['set1', 'set2', 'set3', 'set4']


### Rules ###

include: "rules/src-data.smk"
include: "rules/fastani.smk"
include: "rules/gambit.smk"
include: "rules/db-pw-dists.smk"
include: "rules/figures.smk"
include: "rules/supplemental.smk"


# Create primary figures and tables.
rule main:
	input:
		*rules.figure_1.output,
		*rules.figure_2.output,
		*rules.figure_3.output,
		*rules.figure_6.output,
	output: touch('results/.main-completed')
	default_target: True

# Create supplemental figures and tables.
rule supplemental:
	input:
		*rules.supplemental_figure_1.output
	output: touch('results/.supplemental-completed')

# Create all figures and tables.
rule all:
	input:
		*rules.main.output,
		*rules.supplemental.output,


# Starts IPython shell with access to global scope of workflow. For testing/development.
rule debug:
	run:
		import IPython
		IPython.embed()
